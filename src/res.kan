import ":std/io";
import ":std/dbg";

import "sdl";
import "util";
import "graphics" as g;

type Resource enum {
    PlayerSpriteSheet,
    CatSpriteSheet,
    GirlSpriteSheet,
    GrandmaSpriteSheet,
    BackgroundSpriteSheet,
    ClockSpriteSheet,
    HouseSpriteSheet
}

// why am i using an unfinished language btw?
let spritesheet_specs: [7]g.SpriteSheetSpec = undefined;
let spritesheets: [7]g.SpriteSheet = undefined;

def init_spritesheet_specs() {
    let specs_array_len = util.sizeof_val(&spritesheet_specs) / sizeof g.SpriteSheetSpec;
    let sheet_array_len = util.sizeof_val(&spritesheets) / sizeof g.SpriteSheet;

    dbg.assert(specs_array_len == sheet_array_len, "you forgot to change the arrays again");
    dbg.assert(specs_array_len == Resource.len as usize, "you forgot to change the arrays again");

    let i = -1;
    spritesheet_specs[i += 1] = g.spritesheet_spec(16, 16, "res/player.png");
    spritesheet_specs[i += 1] = g.spritesheet_spec(16, 16, "res/cat.png");
    spritesheet_specs[i += 1] = g.spritesheet_spec(16, 16, "res/girl.png");
    spritesheet_specs[i += 1] = g.spritesheet_spec(16, 16, "res/grandma.png");
    spritesheet_specs[i += 1] = g.spritesheet_spec(8, 8, "res/background.png");
    spritesheet_specs[i += 1] = g.spritesheet_spec(16, 32, "res/clock.png");
    spritesheet_specs[i += 1] = g.spritesheet_spec(80, 64, "res/house.png");
}

def load_spritesheets(renderer: *sdl.Renderer) {
    for let i = 0; i < Resource.len; i += 1 {
        let spec = spritesheet_specs[i];
        if !g.load_spritesheet_from_png(renderer, spec, &spritesheets[i]) {
            io.printf("Could not load sprite: %s\n", sdl.get_error());
            return;
        }
    }
}

def get(res: Resource): *g.SpriteSheet {
    return &spritesheets[res as i32];
}

def free() {
    for let i = 0; i < Resource.len; i += 1 {
        spritesheets[i].free();
    }
}
