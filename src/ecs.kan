import "vec";
import "ptrvec";
import "components";
import "systems" as sys;

type Entity struct {
    value: u32
}

def (e: Entity) eq(other: Entity): bool {
    return e.value == other.value;
}

// a simple bitmap, which marks which components belong to an entity
type Signature struct {
    // this must have at least ComponentKind.len bits
    bits: u32
}

def signature(): Signature {
    return Signature { bits: 0 };
}

def (s: Signature) with(component: components.ComponentKind): Signature {
    let mask = 1 << (component as i32);
    s.bits |= mask as u32;
    return s;
}

def (s: Signature) has_all(other: Signature): bool {
    return s.bits & other.bits == other.bits;
}

def (s: Signature) has_single(component: components.ComponentKind): bool {
    return s.bits & (1 << (component as i32)) != 0;
}

let num_ents: usize = 4096;
let entity_list_data: [4096]Entity = undefined;

// this is an easy way for systems to query the ecs without allocating additional memory
type EntityList struct {
    entities: *Entity,
    len: usize
}

type ECS struct {
    // accessed by Entity.value, holds Signatures
    signatures:     vec.Vec,    // vec.Vec[Signature]
    available_ents: vec.Vec,    // vec.Vec[Entity]
    components:     vec.Vec,    // vec.Vec[ComponentList]
    systems:        ptrvec.Vec  // ptrvec.Vec[*sys.System]
}

def create_ecs(): ECS {
    let num_component_variants = components.ComponentKind.len as usize;

    let ecs = ECS {
        signatures:     vec.with_cap(sizeof Signature, num_ents),
        available_ents: vec.with_cap(sizeof Entity, num_ents),
        components:     vec.with_cap(sizeof ComponentList, num_component_variants),
        systems:        ptrvec.create()
    };

    for let i: usize = 0; i < num_component_variants; i += 1 {
        ecs.set_component_list(i, create_component_list());
    }

    for let i: usize = 0; i < num_ents; i += 1 {
        let e = Entity { value: i as u32 };
        ecs.available_ents.push(&e as *void);
        let sig = Signature { bits: 0 };
        ecs.signatures.push(&sig as *void);
    }

    return ecs;
}

def (ecs: *ECS) free() {
    ecs.signatures.free();
    ecs.available_ents.free();

    for let i: usize = 0; i < ecs.num_systems(); i += 1 {
        ecs.get_system(i).free();
    }
    ecs.systems.free();

    for let i: usize = 0; i < ecs.components.len; i += 1 {
        let cl = ecs.components.get_ptr(i) as *ComponentList;
        cl.free();
    }

    ecs.components.free();
}

def (ecs: *ECS) set_component_list(i: usize, cl: ComponentList) {
    ecs.components.set(i, &cl as *void);
}

def (ecs: *ECS) create_entity(): Entity {
    let e: Entity = undefined;
    ecs.available_ents.pop_last(&e as *void);
    return e;
}

def (ecs: *ECS) entities_with_components(required: Signature): EntityList {
    let list = EntityList { entities: &entity_list_data[0], len: 0 };
    for let i: usize = 0; i < ecs.signatures.len; i += 1 {
        let sig = *(ecs.signatures.get_ptr(i) as *Signature);
        if sig.has_all(required) {
            entity_list_data[list.len] = Entity { value: i as u32 };
            list.len += 1;
        }
    }
    return list;
}

def (ecs: *ECS) add_component_to_entity(e: Entity, kind: components.ComponentKind, c: *void) {
    let cl = ecs.components.get_ptr(kind as i32 as usize) as *ComponentList;
    cl.add_component_to_entity(e, c);
    let mask = 1 << (kind as i32);

    let sig = ecs.signatures.get_ptr(e.value as usize) as *Signature;
    sig.bits |= mask as u32;

    // update systems
    for let i: usize = 0; i < ecs.num_systems(); i += 1 {
        let sys = ecs.get_system(i);
        if (*sig).has_all(sys.relevant_components) {
            sys.entities.push(&e as *void);
        }
    }
}

def (ecs: *ECS) has_component(e: Entity, kind: components.ComponentKind): bool {
    let sig = *(ecs.signatures.get_ptr(e.value as usize) as *Signature);
    return sig.has_single(kind);
}

def (ecs: *ECS) get_component(e: Entity, kind: components.ComponentKind): *void {
    let cl = ecs.components.get_ptr(kind as i32 as usize) as *ComponentList;
    return cl.get_component(e);
}

def (ecs: *ECS) add_system(s: *sys.System) {
    ecs.systems.push_ptr(s as *void);
}

def (ecs: *ECS) num_systems(): usize {
    return ecs.systems.len;
}

def (ecs: *ECS) get_system(i: usize): *sys.System {
    return ecs.systems.get(i) as *sys.System;
}

// ComponentList[C]
type ComponentList struct {
    inner:      ptrvec.Vec, // ptrvec.Vec[C]
    ent_to_idx: vec.Vec,    // vec.Vec[usize]
    idx_to_ent: vec.Vec     // vec.Vec[Entity]
}

def create_component_list(): ComponentList {
    let cl =  ComponentList {
        inner:      ptrvec.create(),
        ent_to_idx: vec.with_cap(sizeof usize, num_ents),
        idx_to_ent: vec.with_cap(sizeof Entity, num_ents)
    };

    // set unused elements to 0b11111...
    cl.ent_to_idx.memset_unused(~0);
    cl.idx_to_ent.memset_unused(~0);

    return cl;
}

def (cl: *ComponentList) free() {
    cl.inner.free();
    cl.ent_to_idx.free();
    cl.idx_to_ent.free();
}

def (cl: *ComponentList) add_component_to_entity(e: Entity, c: *void) {
    let idx = cl.inner.len;
    cl.inner.push_ptr(c);
    cl.ent_to_idx.set(e.value as usize, &idx as *void);
    cl.idx_to_ent.set(idx, &e as *void);
}

def (cl: *ComponentList) get_component(e: Entity): *void {
    let idx = cl.ent_to_idx.get_ptr(e.value as usize) as *usize;
    return cl.inner.get(*idx);
}
