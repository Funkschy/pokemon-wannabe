import "libc";
import "dbg";

type Vec struct {
    len: usize,
    cap: usize,
    elem_size: usize,
    elements: *void
}

def create(elem_size: usize): Vec {
    return Vec { len: 0, cap: 0, elem_size: elem_size, elements: null };
}

def with_cap(elem_size: usize, cap: usize): Vec {
    if cap == 0 {
        return create(elem_size);
    }
    return Vec { len: 0, cap: cap, elem_size: elem_size, elements: libc.malloc(cap * elem_size)};
}

def (v: *Vec) push(elem: *void) {
    let place = v.reserve_single();
    libc.memcpy(place, elem, v.elem_size);
    v.len = v.len + 1;
}

def (v: *Vec) push_front(elem: *void) {
    if v.len + 1 > v.cap {
        v.resize();
    }

    if v.len > 0 {
        libc.memmove(v.elements + v.elem_size, v.elements, v.elem_size * v.len);
    }

    libc.memcpy(v.elements, elem, v.elem_size);
    v.len = v.len + 1;
}

def (v: *Vec) init_zero() {
    v.len = v.cap;
    libc.memset(v.elements, 0, v.elem_size * v.cap);
}

def (v: *Vec) set(i: usize, elem: *void) {
    dbg.assert(i >= 0, "out of bounds set");
    while i >= v.cap {
        v.resize();
    }

    libc.memcpy(v.elements + i * v.elem_size, elem, v.elem_size);
    // assume all values in between to be initialized
    if i >= v.len {
        v.len = i + 1;
    }
}

def (v: *Vec) memset_unused(value: u32) {
    if v.len < v.cap {
        v.memset(v.len, v.cap, value);
    }
}

// [start, end)
def (v: *Vec) memset(start_idx: usize, end_idx: usize, value: u32) {
    dbg.assert(start_idx < end_idx, "start_idx >= end_idx");
    let start = v.elements + start_idx * v.elem_size;
    let len = (end_idx - 1) * v.elem_size;
    libc.memset(start, value, len);
}

def (v: *Vec) get(i: usize, ptr: *void): bool {
    if i >= 0 && i < v.len {
        libc.memcpy(ptr, v.elements + i * v.elem_size, v.elem_size);
        return true;
    }

    return false;
}

def (v: *Vec) get_ptr(i: usize): *void {
    if i >= 0 && i < v.len {
        return v.elements + i * v.elem_size;
    }

    return null;
}

def (v: *Vec) pop_last(mem: *void) {
    dbg.assert(v.len > 0, "popping empty vector");
    libc.memcpy(mem, v.get_ptr(v.len - 1), v.elem_size);
    v.len -= 1;
}

def (v: *Vec) resize() {
    if v.elements == null {
        v.cap = 2;
        v.elements = libc.malloc(v.cap * v.elem_size);
    } else {
        v.cap = v.cap * 2;
        v.elements = libc.realloc(v.elements, v.cap * v.elem_size);
    }
}

// reserve enough memory for the next insertion and return a pointer to the memory, where the next
// element would be inserted
def (v: *Vec) reserve_single(): *void {
    if v.len + 1 > v.cap {
        v.resize();
    }

    return v.elements + v.len * v.elem_size;
}

def (v: *Vec) clone(): Vec {
    let new_v = *v;
    new_v.elements = libc.malloc(v.cap * v.elem_size);
    libc.memcpy(new_v.elements, v.elements, v.cap * v.elem_size);
    return new_v;
}

def (v: *Vec) free() {
    delete v.elements;
}
