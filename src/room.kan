import "io";
import "str";
import "std";
import "npc";
import "sdl";
import "text";
import "ptrvec";
import "config";
import "objects" as o;
import "graphics" as g;

type TileState enum {
    Empty, Object, HasNpc, HasDialogNpc
}

type Tile struct {
    kind: TileState,
    data: *void
}

type Room struct {
    width_units: u32,
    height_units: u32,
    text_stopped_at: usize,
    ground: g.SpriteSheet,
    current_ground: g.Sprite,
    current_wall: g.Sprite,
    tiles: *Tile,
    current_text: str.View,
    textbox: text.TextBox,
    npcs: ptrvec.Vec, // ptrvec.Vec<*npc.Npc>
    objects: ptrvec.Vec // ptrvec.Vec<*o.Object>
}

def create(renderer: *sdl.Renderer, width_units: u32, height_units: u32): Room {
    let spec = g.spritesheet_spec(config.tile_size() as i32, config.tile_size() as i32, "res/background.png");
    let background_spritesheet: g.SpriteSheet = undefined;
    if !g.load_spritesheet_from_png(renderer, spec, &background_spritesheet) {
        io.printf("Could not load sprite: %s\n", sdl.get_error());
        return undefined;
    }

    return Room {
        width_units: width_units,
        height_units: height_units,
        text_stopped_at: 0,
        ground: background_spritesheet,
        current_ground: background_spritesheet.get(1, 0),
        current_wall: background_spritesheet.get(0, 2),
        tiles: std.calloc((width_units * height_units) as usize, sizeof Tile) as *Tile,
        current_text: str.view(0, ""),
        textbox: text.load(renderer),
        npcs: ptrvec.create(),
        objects: ptrvec.create()
    };
}

def (r: *Room) free() {
    delete r.tiles;
    for let i: usize = 0; i < r.npcs.len; i += 1 {
        let npc = r.npcs.get(i) as *npc.Npc;
        npc.free();
    }
    for let i: usize = 0; i < r.objects.len; i += 1 {
        delete r.objects.get(i);
    }
    r.npcs.free();
    r.objects.free();
    r.textbox.free();
    r.ground.free();
}

def (r: *Room) add_object(obj: *o.Object) {
    r.objects.push_ptr(obj as *void);
    let obj_rect = obj.tile_rect();

    for let y = obj_rect.y; y < obj_rect.y + obj_rect.h; y += 1 {
        for let x = obj_rect.x; x < obj_rect.x + obj_rect.w; x += 1 {
            let idx = y as u32 * r.width_units + x as u32;

            *(r.tiles + idx) = Tile { kind: TileState.Object, data: obj as *void };
        }
    }
}

def (r: *Room) add_npc(new_npc: *npc.Npc) {
    r.npcs.push_ptr(new_npc as *void);
    let npc_rect = new_npc.tile_rect();
    let kind = TileState.HasNpc;
    if new_npc.kind == npc.Kind.WithDialog {
        kind = TileState.HasDialogNpc;
    }

    for let y = npc_rect.y; y < npc_rect.y + npc_rect.h; y += 1 {
        for let x = npc_rect.x; x < npc_rect.x + npc_rect.w; x += 1 {
            let idx = y as u32 * r.width_units + x as u32;

            *(r.tiles + idx) = Tile { kind: kind, data: new_npc as *void };
        }
    }
}

def (r: *Room) get_tile(x: u32, y: u32): Tile {
    let idx = y * r.width_units + x;
    return *(r.tiles + idx);
}

def (r: *Room) set_text(text: str.View) {
    r.current_text = text;
    r.text_stopped_at = 0;
}

def (r: *Room) scroll_text(): bool {
    if r.current_text.len == 0 || r.text_stopped_at == 0 {
        return false;
    }

    r.current_text.data += r.text_stopped_at;
    r.current_text.len -= r.text_stopped_at;
    return r.current_text.len == 0 || r.text_stopped_at == 0;
}

def (r: *Room) render_background(renderer: *sdl.Renderer) {
    let sprite = &r.current_wall;
    let scale = config.tile_size() * config.scale() as i32;

    for let y: u32 = 0; y < r.height_units; y += 1 {
        if y > 1 {
            sprite = &r.current_ground;
        }

        for let x: u32 = 0; x < r.width_units; x += 1 {
            let dest = sdl.Rect {
                x: x as i32 * scale,
                y: y as i32 * scale,
                w: scale,
                h: scale
            };
            g.render_sprite(renderer, sprite, dest, sdl.RendererFlip.None);
        }

        // first ground row just rendered
        if y == 2 {
            let line = sdl.Rect {
                x: 0,
                y: scale * 2,
                w: config.pixel_width() as i32,
                h: config.scale() as i32
            };
            renderer.fill_rect(&line);
        }
    }
}

def (r: *Room) render_hud(renderer: *sdl.Renderer) {
    if r.current_text.len == 0 {
        return;
    }

    r.text_stopped_at = r.textbox.render(renderer, r.current_text, 5);
}
